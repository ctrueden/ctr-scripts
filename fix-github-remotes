#!/usr/bin/perl

# fix-github-remotes

# This script ensures that GitHub remotes use the more performant git://
# protocol when fetching, and SSH (git@...) protocol when pushing.

use strict;

my @remotes = `git remote -v`;

# parse remotes
my %fetch = ();
my %push = ();
for my $remote (@remotes) {
  chop $remote;
  (my $name, my $url, my $type) = split(/\s/, $remote);
  if ($type eq '(fetch)') {
    $fetch{$name} = $url;
  }
  elsif ($type eq '(push)') {
    $push{$name} = $url;
  }
  else {
    print STDERR "Warning: ignoring remote '$remote' of unknown type\n";
  }
}

my $matchPattern = '^(git|https?|ssh|ssh+git)(\@|:\/\/)github.com([:\/])(.*)$';
my $pushPrefix = 'git@github.com:';
my $fetchPrefix = 'git://github.com/';

for my $name (keys %fetch) {
  if (not defined $push{$name}) {
    print STDERR "Warning: ignoring remote '$name' with no push URL\n";
    next;
  }
  my $fetchURL = $fetch{$name};
  my $pushURL = $push{$name};

  if ($fetchURL =~ /$matchPattern/) {
    # convert fetch URL to proper protocol
    $fetchURL = $fetchPrefix . $4;
    if ($fetchURL =~ /\.git$/) {
      # remote superfluous .git suffix
      $fetchURL = substr $fetchURL, 0, -4;
    }
  }
  elsif ($fetchURL =~ /github.com/) {
    print STDERR "Warning: for remote '$name': " .
      "ignoring GitHub fetch URL of unknown format: $fetchURL\n";
  }

  if ($pushURL =~ /$matchPattern/) {
    # convert push URL to proper protocol
    $pushURL = $pushPrefix . $4;
    if ($pushURL =~ /\.git$/) {
      # remote superfluous .git suffix
      $pushURL = substr $pushURL, 0, -4;
    }
  }
  elsif ($pushURL =~ /github.com/) {
    print STDERR "Warning: for remote '$name': " .
      "ignoring GitHub push URL of unknown format: $pushURL\n";
  }

  # use git to update the remotes
  my $fetchChanged = 0;
  if ($fetch{$name} ne $fetchURL) {
    # fetch URL does not match; update it
    my $cmd = "git remote set-url $name $fetchURL";
    print "$cmd\n";
    `$cmd`;
    $fetchChanged = 1;
  }
  if ($fetchChanged || $push{$name} ne $pushURL) {
    # push URL does not match; update it
    my $cmd = "git remote set-url --push $name $pushURL";
    print "$cmd\n";
    `$cmd`;
  }
}
